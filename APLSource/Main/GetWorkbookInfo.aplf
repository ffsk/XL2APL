 res←GetWorkbookInfo w;a;p;get;r;ri;rt;m;at;id;fn;k;d;f;se
     ⍝ w ←→ FQP of .xlsx file
     ⍝   ←→ Space of usefull stuff
     a←OpenExcelFile w
     p←⎕NS''
     p.FileName←w
     get←{0=≢⍵:'' ⋄ ⍺∘{(⍵[;0]⍳⊂⍺)⊃⍵[;1],⊂''}¨⍵}
     r←⎕XML a ReadZipFile'xl/_rels/workbook.xml.rels'
     ri←'Id'get r[;3]
     rt←'Target'get r[;3]
     m←⎕XML a ReadZipFile'xl/workbook.xml'
     at←m[⍸m[;1]∊⊂'sheet';3]
     p.SheetNames←'name'get at
     id←'r:id'get at
     fn←'xl/'∘,¨rt[ri⍳id]
     p.SheetFileNames←fn
     k←⍸m[;1]∊⊂'definedName'
     p.RangeNames←'name'get m[k;3]
     p.RangeAddresses←m[k;2]
     _←a GetFormatCodes p
     d←('/'∊⍕⌷a.Entries)⊃'\/'
     f←d@('/'∘=)
     se←{⍵[⍵.FullName⍳f¨fn]}⌷a.Entries
     :Select Platform.arch
     :Case '.NET'
         p.Lengths←Convert.ToInt32¨se.Length
         p.CompressedLengths←Convert.ToInt32¨se.CompressedLength

     :Case '.NET Core'
         ⍝ .NET Core behaves slightly different than .NET
         p.Lengths←,se.Length
         p.CompressedLengths←,se.CompressedLength
     :EndSelect
     p.BlockSize←2*22
     p.HeaderRows←1
     p.FooterRows←0
     p.RowsToRead←0
     _←Dispose a
     res←p
